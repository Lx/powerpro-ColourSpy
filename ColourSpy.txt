; ColourSpy v1.2
; Written by Alex Peters, 22/8/2003
; ______________________________________________________________________

@Toggle

    Static ColourSpyEventHandle

    If(Event.Exists(ColourSpyEventHandle)) Do
        Event.Destroy(ColourSpyEventHandle)
        ColourSpyEventHandle = ""
        *Bar Close ColourSpy
    Else
        ColourSpyEventHandle = Event.CreateMS(1, 0, '+
            "If(Ctrl)'r.ColourSpy@Update")
        Static ColourSpyBegin = 0
        *Bar Show ColourSpy
    EndIf

Quit

@Update

    Static Pixel = Win.GetPixel(XMouse, YMouse)
    Static MousePos = XMouse ++ " " ++ YMouse

    Do("Format Item List ColourSpy Item 4 Text", "'"" ++ '+
        Word(MousePos, 1) ++ ", " ++ Word(MousePos, 2) ++ "'"")

    Do("Format Item List ColourSpy Item 7 OwnBack 1 " ++ '+
        "ColorBack", (256 * ((256 * Word(Pixel, 3)) + Word(Pixel, '+
        2))) + Word(Pixel, 1))

    Do("Format Item List ColourSpy Item 9 Text", " " ++ '+
        Word(Pixel, 1) ++ ",")
    Do("Format Item List ColourSpy Item 10 Text", Word(Pixel, '+
        2) ++ ",")
    Do("Format Item List ColourSpy Item 11 Text", Word(Pixel, '+
        3))

    Static Hex = Fill("#000000", .ColourSpy@Hex((256 * ((256 * '+
        Word(Pixel, 1)) + Word(Pixel, 2))) + Word(Pixel, 3)))
    Do("Format Item List ColourSpy Item 14 Text", " " ++ Hex)

    If(Not ColourSpyBegin) Do

        ColourSpyBegin = 1

        For(Local Iter = 4; Iter < 16; Iter = Iter + 1)
            Do("*Format Item List ColourSpy Hide 0 Item", Iter)
        EndFor
        *Format Item List ColourSpy Item 16 Hide 1

    EndIf

Quit

@CopyRGB

    Static Pixel
    Clip.Set("R: " ++ Word(Pixel, 1) ++ "; G: " ++ Word(Pixel, 2) ++ '+
        "; B: " ++ Word(Pixel, 3))

Quit

@CopyHex

    Static Hex
    Clip.Set(Hex)

Quit

@CopyPos

    Clip.Set("X: " ++ Word(MousePos, 1) ++ "; Y: " ++ '+
        Word(MousePos, 2))

Quit

@Hex

    If(Arg(1) < 16)
        Quit(Select("0123456789ABCDEF", Arg(1) + 1, Arg(1) + 1)

    Local Mod16 = (Arg(1) % 16) + 1

Quit(.ColourSpy@Hex(Arg(1) / 16) ++ Select("0123456789ABCDEF", Mod16, '+
    Mod16))
