; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; ColourSpy v2.0 Installation
; Written by Alex Peters, 10/11/2003

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; The installation script (and also ColourSpy itself) requires that the
; ‘Use Quote for Escape in Expression Strings’ option (found under
; Configuration > Setup > Advanced Setup > Characters) is ticked. If
; this option is not ticked, we must abort the installation and let the
; user know how to set the option.

If(Length("'r") == 2) Do
    MessageBox("Stop", "Installation cannot continue until a PowerPro setting is changed. Please enter Configuration > Setup > Advanced Setup > Characters, tick the ‘Use Quote for Escape in Expression Strings’ option, and then try again.", "ColourSpy v2.0 Installation")
    Quit
EndIf

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; Ensure that all needed files for installation exist.

For(Local FileCheck = 1; FileCheck <= 4; FileCheck = FileCheck + 1)
    If(Not(ValidPath(ScriptFolder ++ "\Install.00" ++ FileCheck))) Do
        MessageBox("Stop", "The following file could not be found:'nInstall.00" ++ FileCheck ++ "'n'nInstallation cannot continue without this file. Please extract the contents of the archive into a folder and try again.", "ColourSpy v2.0 Installation")
        Quit
    EndIf
EndFor

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; Briefly describe the ColourSpy bar's purpose and ask if installation
; should continue. If the user clicks ‘No’, abort the installation.

If(MessageBox("Information YesNo", "The ColourSpy bar displays RGB and hex values of pixels underneath the mouse cursor.'n'nWould you like to continue with the installation?", "ColourSpy v2.0 Installation") == 7)
    Quit

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; Attempting to automatically modify the PowerPro configuration seems to
; be a dangerous practice while PowerPro Configuration is open, since
; pressing OK or Cancel after the installation has made its changes will
; result in those changes being undone. We can prevent the script from
; continuing until PowerPro Configuration is closed, so that this can't
; happen.

For(AnyWindow("=PProConf"))
    If(Not(MessageBox("Stop OKCancel", "Installation cannot continue while the PowerPro Configuration Editor is running.'n'nPlease close it and click OK to continue.", "ColourSpy v2.0 Installation")))
        Quit
EndFor

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; This is an attempt at figuring out which configuration is running.
; PowerPro provides us with the folder of the currently running PowerPro
; configuration, but not the file itself. We can scan this given folder
; for .PCF files, and if only one .PCF file exists in this folder, then
; that file is the current configuration and we can bet that this is the
; one to which we wish to install ColourSpy. If there are more than one
; .PCF file, however, then we can present a combo box and allow the user
; to select the correct file.

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; We are excluding all .PCF files that begin with ‘!Auto backup of’ and
; ‘!Previous auto backup’, since it is unlikely that the user will want
; to install ColourSpy to one of these configurations.

Static PCFList = ""
File.AllFiles(PProFolder ++ "*.PCF", ".@ProcessPCFs('"|'")")

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; If there is more than one PowerPro configuration in the given folder,
; then we will present a list of each configuration, with the first file
; in the list selected by default.

Local PCF = Select(PCFList, Index(PCFList, "|") - 1)

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; If a configuration by the name of ‘PProConf.pcf’ exists, however, then
; we will select that by default since it's most likely that that will
; be the desired configuration for installation.

If(ValidPath(PProFolder ++ "PProConf.pcf"))
    PCF = PProFolder ++ "PProConf.pcf"

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; Present a dialogue asking for the path to the desired configuration
; for installation. If more than one configuration was found in the
; current configuration's folder, then display a combo box that contains
; the names of each configuration found; otherwise, display the current
; configuration as default.

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; Keep presenting this dialogue until either a valid .PCF file is
; specified, or the user cancels the installation.

PCFList = Remove(PCFList, -1)

For()
    If(Not(InputDialog("PCF=Please select the .PCF file to which to install ColourSpy:" ++ IfElse(Index(PCFList, "|"), "??" ++ PCFList), "ColourSpy v2.0 Installation")))
        Quit
    If(ValidPath(PCF))
        Break
    If(Not(MessageBox("Warning OKCancel", "This file does not exist:'n" ++ PCF ++ "'n'nPlease enter an existing PowerPro configuration.", "ColourSpy v2.0 Installation")))
        Quit
EndFor

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; We will now check the specified PCF file for an existing command list
; named ‘ColourSpy’, to prevent accidental overwriting. We will do this
; by exporting every bar from this configuration to a text file, and
; then searching that text file for a ColourSpy command list.

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; PowerPro must be running in order for this installation script to be
; running. Therefore, we can find the full path of PowerPro, and hence
; PProConf, which we need in order to export the configuration's bars.

Local PowerProPath = Win.EXEPath("=PowerPro")
Local PProConfPath = Select(PowerProPath, RevIndex(PowerProPath, "\")) ++ "PProConf.exe"
Local BarCheckPath = ScriptFolder ++ "\BarCheck.txt"
Do(PProConfPath, "'"" ++ PCF ++ "'" /L:'"" ++ BarCheckPath ++ "'"")

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; We're waiting for PProConf to start and finish writing to this text
; file, so that we know when to continue. Then, we'll read the file into
; a variable and do away with the file.

For(Not(ValidPath(BarCheckPath)))
*Wait Sleep 250
EndFor

For(ValidPath(BarCheckPath))
    Local BarCheck = File.ReadAll(BarCheckPath)
    File.DeleteNoRecycle(BarCheckPath)
*Wait Sleep 250
EndFor

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; If the configuration contains a ColourSpy command list, display a
; confirmation box allowing the user to abort the installation.

If(Index(BarCheck, "'r'n[**ColourSpy]'r'n")) Do
    If(MessageBox("Question YesNo", "A command list named ‘ColourSpy’ already exists in the PowerPro configuration that you selected.'n'nDo you wish to overwrite this command list?", "ColourSpy v2.0 Installation") == 7)
        Quit
EndIf

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; Import the ColourSpy command list into the configuration.

Do(PProConfPath, "'"" ++ PCF ++ "'" /I:'"" ++ ScriptFolder ++ "\Install.001'"")

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; Install the ColourSpy script into the Scripts folder, creating the
; folder if necessary.

If(Not(ValidPath(PProFolder ++ "Scripts")))
    Do("*Exec NewFolder", PProFolder ++ "Scripts")
Do("*File Copy", "'"" ++ ScriptFolder ++ "\Install.002'" '"" ++ PProFolder ++ "Scripts\ColourSpy.PowerPro'"")

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; Install the ColourSpy skin bitmap and text files into a ColourSpy
; subfolder of the Skins folder, creating the folders as required.

If(Not(ValidPath(PProFolder ++ "Skins")))
    Do("*Exec NewFolder", PProFolder ++ "Skins")
If(Not(ValidPath(PProFolder ++ "Skins\ColourSpy")))
    Do("*Exec NewFolder", PProFolder ++ "Skins\ColourSpy")
Do("*File Copy", "'"" ++ ScriptFolder ++ "\Install.003'" '"" ++ PProFolder ++ "Skins\ColourSpy\Back.bmp'"")
Do("*File Copy", "'"" ++ ScriptFolder ++ "\Install.004'" '"" ++ PProFolder ++ "Skins\ColourSpy\Skin.txt'"")

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; Notify the user that the installation is complete, and that they will
; need to set up a method of showing the bar.

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; BUG: It's possible to offer to open PProConf for the user, but for
;      some strange reason, nothing below the next MessageBox() line is
;      executed -- on at least two XP systems.

MessageBox("Information", "The installation of ColourSpy is now complete.'n'nYou will need to set up a method of showing the ColourSpy bar manually, or you may wish to set it to ‘Auto Show as Bar’.", "ColourSpy v2.0 Installation")
Do(PProConfPath, "'"" ++ PCF ++ "'"")

Quit

; ¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯¯
; This section of the script is called when the PProFolder is scanned
; for .PCF files. It removes unnecessary entries from the list.

@ProcessPCFs

    Local FileOnly = Remove(Arg(1), RevIndex(Arg(1), "\"))
    If(Index(FileOnly, "!Auto backup of ") == 1)
        Quit
    If(Index(FileOnly, "!Previous auto backup ") == 1)
        Quit
    Static PCFList = PCFList ++ Arg(1) ++ "|"
    Quit
; ______________________________________________________________________